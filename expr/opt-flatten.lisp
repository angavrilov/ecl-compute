;;; -*- mode:lisp; indent-tabs-mode: nil; -*-

(in-package fast-compute)

(use-std-readtable)

;;; Select a suitable representation based on the list length
(defun wrap-assoc-op (lst op zero)
  (cond ((null lst) zero)
        ((null (cdr lst)) (car lst))
        (t (list* op lst))))

;;; Combine values, ignoring nil
(defun reduce-non-nil (op a b)
  (cond ((null a) b)
        ((null b) a)
        (t (funcall op a b))))

;;; Remove items that are symmetric wrt inv-func
(defun cancel-items (items inv-func)
  (let* ((ritems (image #f(canonic (funcall inv-func) _) items))
         (common (intersection items ritems)))
    (if (empty? common)
        items
        (progn
          (format t "Cancelled out:誉ア磲疸狎ｆㄣ犷镱殂轭趄轹獒扉瀛蝈骟擤ㄣ镱鲥螋ъ轶泔眄镱┅ㄢ徵溟骀弪孱沐轸屙泔眄镱┅┅换蔑祆邈翦蝽麒殪泔礅轭轭铛礅弪犷蝈盹鲩铉蝈漉钿犷镱弩ㄤ彐躅泔祆邈舡翦蝽蝈漉沐镳轭霏镳翎绛镳弪屮痱脲铛祆鲠祯濠戾è轸屙ㄥ眇豉忉绌鲠祯铋飑ㄤ镬轶ㄩ翦屮痱螬ㄩ铛礅弪轸屙箦翩鲠祯蝈漉沐铒瞽铋蝈漉沐镳鲠祯轸屙┅ㄡ潢镩铈轸屙磲脲汜铒铋轸屙┅┅ㄩㄡ钿铛祆鲠祯鲠祯鲠祯铛祆鲠祯濠痱镧ㄦ矧磲⒁邃蹉邃麸梁誉ア铛祆鲠祯磲疸狎＇趄轹獒扉瀛蝈骟屮痱螬铛祆鲠祯濠戾è轸屙ㄣ犷沐飙轸屙轸屙轭霏镳┅麒孱ㄡ钿鲠祯铒鲠祯弪铹┅ㄡ潢镩铈轸屙鲠祯濠黩狃狍箫悱镳ㄣ犷镱殂忉绛麸扉篝轸屙螬翎绛镳弪铹┅┅换旗狒翦铋铉镦徜溟糸镱ㄤ彐躅麸珑戾黹铛ㄥ痱磲翥屮痱è豉疱铛礅弪铛愆ō铛愆ㄠō啜┅┅ㄤ彐躅泔祆邈臬ㄡ蜱螬ㄣ镬戾泗翦蝽＇＇麸珑戾黹铛Й狎珞┅ㄤ彐躅骒狒翦瞰狎珞ㄡ蜱螬磲疸犷＇灬礅溽ㄡ蜱磲翥狎ㄠǐ漓篝ㄣ镳扉篝祗舂ㄠō豉疱铛礅弪鲠飑扉篝ō鲠飑┅ㄠōǐ漓篝┅磲疸狎＇麸珑戾黹铛祗舂ㄟ扉篝狎绌┅狎珞┅ㄤ彐躅骒狒翦瞰ㄡ蜱螬ㄣ镬戾泗ㄦ灬趑孱狎珞狎珞┅换旗狒翦铋铉镦眭祠轲扉汜糸镱ㄤ彐躅麸珑戾溟ㄥ痱磲翥屮痱ū暴ㄠǒ啜┅┅ㄤ彐躅泔祆邈舄ㄡ蜱螬ㄣ镬戾泗翦蝽＇＇麸珑戾溟И狎珞侯蹯飙鲠祯癌ㄤ彐躅骒狒翦瞠狎珞ㄡ蜱螬磲疸犷＇灬礅溽ㄡ蜱磲翥狎ㄠí漓篝ㄣ镳扉篝祗舂ㄠǒí漓篝┅磲疸狎＇麸珑戾溟祗舂ㄠōí漓篝┅ㄣ镱麸珑戾黹铛ㄣ镳扉篝祗舂┅ㄠōǒí漓篝┅磲疸狎＇麸珑戾溟ㄣ镱麸珑戾黹铛ㄣ镳扉篝祗舂┅ㄟ扉篝狎绌┅狎珞┅ㄤ彐躅骒狒翦瞠ㄡ蜱螬ㄣ镬戾泗ㄦ灬趑孱狎珞狎珞┅换旗狒翦铋铉麽祀弪ㄤ彐蝈黩轸瀛疳篌骒狒翦瞽屮痱ê汜铒铋舂ㄠǐㄠíㄠōō┅ㄠǒǒ┅ㄠō豉疱铛礅弪鲠飑ō鲠飑ㄠō擤屮痱ㄠǒ擤屮痱ㄠō莉弩舂ㄦ灬趑孱啜ōㄦ灬趑孱蝈篝┅┅ㄠǐ泪蜱螬ㄦ灬趑孱狎珞┅ㄠǒ莉弩舂ㄦ灬趑孱啜ǒㄦ灬趑孱蝈篝┅┅ㄠí泪蜱螬ㄦ灬趑孱狎珞┅